#!/usr/bin/perl
##########
# spectest-same.pl - find identical spectests between two rakudo dirs
#
# Usage:
#    bin/spectest-same.pl dir0 dir1
#
# Description:
#    This program looks at the t/spectest.data files in two rakudo
#    directories passed as arguments to the script, then prints out
#    the entries where the corresponding spectest is identical in
#    each.
##########

use warnings;
use strict;
use File::Compare;

# Check that we've been given two arguments
@ARGV == 2 || die "Usage: $0 <dir> <dir>\n";

my $rel0 = $ARGV[0];
my $rel1 = $ARGV[1];

# Read the filenames in <dir0>/t/spectest.data into %list0
my %list0;
open(my $fh, "<", "$rel0/t/spectest.data") 
    or die "Could not read $rel0/t/spectest.data: $!";
while (<$fh>) {
    next if /^\s*#/;
    next unless /(\S+\.t)/;
    $list0{$1}++;
}
close($fh);

# Read the lines in <dir1>/t/spectest.data, skip over any files
# not listed in %list0 (from above), and put the spectest line
# into @common if the two files compare the same.
my @common;
open($fh, "<", "$rel1/t/spectest.data")
    or die "Could not read $rel1/t/spectest.data: $!";
while (<$fh>) {
    next if /^\s*#/;
    next unless /(\S+\.t)/;
    next unless $list0{$1};
    push @common, $_ if compare("$rel0/t/spec/$1", "$rel1/t/spec/$1") == 0;
}
close($fh);

# Now output our results
my $date = localtime;
my $count = 0+@common;
print <<"EOH";
# This is a list of all spec tests that are common between
# $rel0 and $rel1.  
# This list is suitable for use as a t/localtest.data file, 
# such that "make localtest" can be used in a rakudo base directory
# to run only the spec tests listed in this file.
#
# Generated by $0 on $date.
# $count files.
EOH
print @common;
print "# end of common spectest list\n";

# done!
0;
